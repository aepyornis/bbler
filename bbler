#!/usr/bin/env fish
##########################################################
# This is a program to download and parse NYC housing data
# It accepts a single argument: a BBL
#
# It grabs information from these sources:
#   - Tax Bills from the department of finance
#   - job info from department of buildings
#
#
# in dob/[job_number]
#      - [job_number].json -> dobjobs information about job
#      - [job_number].html -> downloaded BIZ page for job
#      - pw1.json  -> parsed BIS html
#
# in dof[bbl] 
#     - *.pdf -> downloaded tax bills
#     - tax_bills.json -> array of parsed tax bills
#
# in bbl/[bbl] 
#     - dobjobs.json n-> dob jobs information (from dobjobs table in nycdb)
#     - pluo.json -> json of pluto for bbl
#     - rent_stab.json -> tax bills + compiled stats for the tax bills (see rent-stab.py)
#     - [bbl].json -> all above json combined into one object
##########################################################

##
# Helper functions
source $BBLER_PATH/lib/functions.fish

# Ensure first argument is a properly formated BBL
verify_bbl_input $argv

set BBL $argv[1]

## 
# root folder where data is stored:  ~/.nyc-data
_setup_folder_structure

##
# default paths to housing utility programs
set dof_reader $BBLER_PATH/node_modules/dof-reader/bin/dof-reader.js
set dof_download $BBLER_PATH/lib/dof_download.py
set pw1_download $BBLER_PATH/lib/pw1_download.py
set rent_stab $BBLER_PATH/lib/rent-stab.py

##
# configuration variables
set -l dob_min_date '2016-01-01'

## 
# set folder variables
set dof_folder ~/.nyc-data/dof
set dob_folder ~/.nyc-data/dob
set bbl_folder ~/.nyc-data/bbl

# create the folder for this bbl if it doesn't yet exist
mkdir -p $bbl_folder/$BBL

# variables for files for this BBL:
set dof_file "$dof_folder/$BBL/tax_bills.json"
set rent_stab_file "$bbl_folder/$BBL/rent_stab.json"
set dobjobs_file "$bbl_folder/$BBL/dobjobs.json"
set pluto_file "$bbl_folder/$BBL/pluto.json"
# this file is an aggregate of above
set bbl_file "$bbl_folder/$BBL/$BBL.json"

# if the dof file does not exist or is empty
if not test -s $dof_file
    mkdir -p "$dof_folder/$BBL"

    # Only download dof data if there are less than 9 pdfs 
    # Later we should figure out a better to way let
    # the program now that we want to refresh with the latest tax bills
    if test (find "$dof_folder/$BBL" -name '*.pdf' | wc -l) -lt 9
	# Download DOF data
	python3 $dof_download $BBL $dof_folder
    end
    
    set -l tmp_dof (mktemp)

    # process all PDFs and collect results in an json array
    for pdf in (find $dof_folder/$BBL -type f -name '*Tax_Bill.pdf')
	node $dof_reader $pdf >> $tmp_dof
    end

    cat $tmp_dof | jq --slurp '.' > $dof_file
    rm $tmp_dof
end

if not test -s $rent_stab_file
    python3 $rent_stab $BBL | jq -M '.' > $rent_stab_file
end


# download dobjobs json from nycdb.info if it does not exist
if not test -f $dobjobs_file
    curl -sS -G "https://api.nycdb.info/dobjobs" --data "bbl=eq.$BBL" --data "jobtype=neq.A3" --data "prefilingdate=gte.$dob_min_date" \
    | jq -M '.' > $dobjobs_file
end

# download pluto json
if not test -f $pluto_file
    curl -sS -G "https://api.nycdb.info/pluto_17v1" --data "bbl=eq.$BBL" | jq -M '.[]' > $pluto_file
end

set dob_job_numbers (dobjobs_for $BBL)

# Downloads department of buildings jobs from website
for job_number in $dob_job_numbers
    download_and_parse_job $job_number
end

if not test -s $bbl_file
    set -l bbl_json (echo $BBL | jq '{bbl: . | tostring}')
    set -l pluto (jq '{pluto: .}' $pluto_file)
    set -l dobjobs (jq '{dobjobs: .}' $dobjobs_file)
    set -l dofRentStab (jq '{dof: .}' $rent_stab_file)

    echo "$bbl_json $pluto $dobjobs $dofRentStab" | jq -M --slurp 'add' > $bbl_file
end
